;
; SimH "do" script to assemble all of the components
; for the original PTS BASIC version 1A using 
; PAL-11S papertape assembler
;
; Source (input) tapes are read from the high-speed reader
; Object (output) are punched on the high-speed punch
; Symbol table is output to the console
; Listing is skipped
;
; NG 30-Mar-2024
;

echo
echo ...configure SimH as a minimal PDP-11/70
set cpu 11/70 nofpp 128k
sh cpu

; ...disable undesired devices
SET HK DISABLE
SET DZ DISABLE
SET RL DISABLE
SET RX DISABLE
SET RP DISABLE
SET RQ DISABLE
SET TM DISABLE
SET TQ DISABLE
SET RK DISABLE

; ...enable essential devices
SET PTR ENABLE
SET PTP ENABLE

; force upper-case input as this is what the
; PTS-11 programs expect/require
SET TTI UC

echo
echo ... toggle-in papertape bootstrap loader
; ...key-in paper-tape bootstrap @ 28kW
DEPOSIT 157744 016701
DEPOSIT 157746 000026
DEPOSIT 157750 012702
DEPOSIT 157752 000352
DEPOSIT 157754 005211
DEPOSIT 157756 105711
DEPOSIT 157760 100376
DEPOSIT 157762 116162
DEPOSIT 157764 000002
DEPOSIT 157766 157400
DEPOSIT 157770 005267
DEPOSIT 157772 177756
DEPOSIT 157774 000765
DEPOSIT 157776 177550
DEPOSIT SR 157744

echo
echo ... place Absolute Loader tape in high-speed reader
; ...load Absolute Loader paper-tape in high-speed reader
; Absoluer Loader (1976 version)
attach ptr ./tools/dec-11-uabla-a-po-bin.pt
; skip over garbage at begining of the tape
dep ptr pos 32

echo ... run bootstrap to put loader into core
; ...run (reads Absolute Loader into core)
go 157744
echo
echo ... halt at 157500 indicates success
echo
echo ... load and start assembler PAL-11S
; ...load PAL-11S paper-tape in high-speed reader
attach ptr ./tools/dec-11-uplsa-a-pl-bin.pt

;
; Prepare automated answers to the initial PAL11-S
; questions for Source, Binary, Listing, and symbol Table I/O.
; Use the high-speed reader/punch for source and binary
; Use the TTY for listing and symbol table
;
expect "*S " send "H\r";continue
expect "*B " send "H\r";continue
expect "*L " send "\r";continue
expect "*T " send "T\r";continue

expect "EOF ?" call proc1

goto main

:proc1
echo
echo .....................................
echo ... assemble "basich"
echo .....................................
attach ptp ./obj/basich-obj.pt
attach ptr ./src/basich-mac.pt
sh ptp
sh ptr
send "\r"
expect "END ?" call proc2
continue
return

:proc2
; send CR to indicate end of tape(s) 
send "\r"
expect "EOF ?" call proc3
continue
return

:proc3
echo
echo ... reposition for pass-2
dep ptr pos 0
send "\r"
;
expect "*S " send "H\r";continue
expect "*B " send "H\r";continue
expect "*L " send "\r" ;continue
expect "*T " send "T\r";continue
expect "EOF ?" call proc4
continue
return

:proc4
echo
echo .....................................
echo ... assemble "basicl"
echo .....................................
attach ptp ./obj/basicl-obj.pt
attach ptr ./src/basicl-mac.pt
sh ptp
sh ptr
send "\r\r"
expect "END ?" call proc5
continue
return

:proc5
; send CR to indicate end of tape(s) 
send "\r"
expect "EOF ?" call proc6
continue
return

:proc6
echo
echo ... reposition for pass-2
dep ptr pos 0
send "\r"
;
expect "*S " send "H\r";continue
expect "*B " send "H\r";continue
expect "*L " send "\r" ;continue
expect "*T " send "T\r";continue
expect "EOF ?" call proc7
continue
return

:proc7
echo
echo .....................................
echo ... assemble "fpmp" 
echo .....................................
attach ptp ./obj/fpmp-obj.pt
attach ptr ./src/fpmp-mac.pt
sh ptp
sh ptr
send "\r\r"
expect "END ?" call proc8
continue
return

:proc8
; send CR to indicate end of tape(s) 
send "\r"
expect "EOF ?" call proc9
continue
return

:proc9
echo
echo ... reposition for pass-2
dep ptr pos 0
send "\r"
;
expect "*S " echo;quit
continue
return

;----------------------------------------

:main
;ECHO ...run Absolute Loader to read the binary and start it
;ECHO ...at the transfer address the binary tape indicates
dep sr 157500
go 157500


