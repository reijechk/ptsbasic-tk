; link-reverse-string
;
; This SimH "DO" script links the object tapes for the
; IOX Reverse String example Using the relocatable linker, LINK-11s.
; This involves answering the initial LINK-11s I/O questions, loading
; each tape in sequence for pass-1, then reloading each tape in the
; same sequence for pass-2.
;
; SimH configuration: PDP-11/70 minimal system
; Input tape: high-speed reader
; Output tape: high-speed punch
; Loader map (output): console
; 
; There should be a better way to accomplish this.
; This SimH "DO script" is made awkward by the design of the
; [expect] statement. It is a one-shot recognition so it must
; be recreated with each use. It is also implemented as a 
; "call" not "goto" 
;
; NG 1-Apr-2024
;

ECHO ================
ECHO A basic, essential system for PTS11 use
ECHO ================
ECHO
ECHO ...Set the CPU parameters
set cpu 11/70 nofpp 128k
sh cpu

ECHO ...disable undesired devices
SET HK DISABLE
SET DZ DISABLE
SET RL DISABLE
SET RX DISABLE
SET RP DISABLE
SET RQ DISABLE
SET TM DISABLE
SET TQ DISABLE
SET RK DISABLE

ECHO ...enable essential devices
SET PTR ENABLE
SET PTP ENABLE

; force upper-case input as this is what
; the PTS-11 programs expect/require
SET TTI UC

ECHO ...key-in paper-tape bootstrap @ 28kW
DEPOSIT 157744 016701
DEPOSIT 157746 000026
DEPOSIT 157750 012702
DEPOSIT 157752 000352
DEPOSIT 157754 005211
DEPOSIT 157756 105711
DEPOSIT 157760 100376
DEPOSIT 157762 116162
DEPOSIT 157764 000002
DEPOSIT 157766 157400
DEPOSIT 157770 005267
DEPOSIT 157772 177756
DEPOSIT 157774 000765
DEPOSIT 157776 177550
DEPOSIT SR 157744

ECHO ...load Absolute Loader paper-tape in high-speed reader

; Absolute Loader (1976)
attach ptr ./tools/dec-11-uabla-a-po-bin.pt
dep ptr pos 32

ECHO ...run (reads Absolute Loader into core)
go 157744
ECHO ...halt at 157500 indicates success

ECHO ...load LINK-11S paper-tape in high-speed reader
; LINK-11S (1976)
attach ptr ./tools/dec-11-ulksa-a-pl-bin.pt


;
; Prepare automated answers to the initial LINK11 questions
;   Input      - high speed reader
;   Output     - high speed punch
;   Map        - TTY/Console
;   Mem Top    - default
;   Mem Bottom - default
;
expect "*I " send "H\r" ;cont
expect "*O " send "H\r" ;cont
expect "*M " send "T\r" ;cont
expect "*T " send "\r"  ;cont
expect "*B " send "\r"  ;cont
expect "* "  call proc1

goto main

:proc1
echo ... start of pass 1
attach ptp ./reverse-string-bin.pt
attach ptr ./obj/reverse-string-obj.pt
sh ptp
sh ptr
send "\r"
expect "* " call proc2
continue
return

:proc2
echo
attach ptr ./obj/iox-v2-obj.pt
sh ptr
send "\r"
expect "* " call proc3
continue
return

:proc3
; respond "C" to indicate there are no more tapes and to
; "clear" (set to zero) the address of any unresolved symbols
send "C\r"
expect "* " call proc4
continue
return

;----------------------------------------
; Handle Pass 2

:proc4
echo ... start of pass 2
attach ptr obj/reverse-string-obj.pt
sh ptr
send "\r"
expect "* " call proc5
continue
return

:proc5
echo
attach ptr ./obj/iox-v2-obj.pt
sh ptr
send "\r"
; from pass 1 LINK-11s knowns how many tapes to expect
; so it will not request further tapes but instead
; restart itself when the current link operation completes
; expect the "input source" prompt and exit
expect "*I " echo;quit
continue
return

;----------------------------------------

:main
; ...run Absolute Loader to read the binary and start it
; ...at the transfer address the binary tape indicates
dep sr 157500
go 157500

